#!/usr/bin/env planck

(ns script.run-all
  (:require [planck.core :refer
             [spit slurp sleep read-string *command-line-args*]]
            [planck.shell :refer [sh sh-async with-sh-dir]]
            [planck.io :as io]
            [planck.http :as http]
            [clojure.string :as str]
            [goog.i18n.DateTimeFormat :as dtf]
            [goog.string :as gstring]
            [goog.string.format]))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                                                            ;;
;;                       ----==| U T I L I T Y |==----                        ;;
;;                                                                            ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn format-date-generic
  "Format a date using either the built-in goog.i18n.DateTimeFormat.Format enum
  or a formatting string like \"dd MMMM yyyy\""
  [date-format date]
  (.format (goog.i18n.DateTimeFormat.
            date-format)
           (js/Date. date)))


(defn get-env []
  (into {}
        (map (fn [line]
               (str/split line #"=" 2))
             (-> (:out (sh "env"))
                 (str/split #"\n")))))


(defn path
  [& args]
  (as-> args $
       (str/join "/" $)
       (str/replace $ #"/+" "/")))


(defn msg [& message]
  (if (map? (first message))
    (cond

      (= :huge (:type (first message)))
      (do
        (println "(*)")
        (println "(*) ============================================================")
        (println "(*)")
        (apply msg (map (comp str/upper-case str) (rest message)))
        (println "(*)")
        (println "(*) ============================================================")
        (println "(*)"))

      (= :big (:type (first message)))
      (do
        (println "(*) ------------------------------------------------------------")
        (apply msg (rest message))
        (println "(*) ------------------------------------------------------------"))

      :else
      (prn "(*)" message))
    (apply println "(*)" message)))


(def is-debug? (get (get-env) "DEBUG"))

(defn debug [& message]
  (when is-debug?
    (apply println "(D)" message)))


(defn sh! [cmd]
  (let [args (str/split cmd #" +")
        _    (debug "running1: " cmd)
        _     (debug "running2: " (pr-str args))
        out  (apply sh args)]
    (when (not= 0 (:exit out))
      (throw (ex-info (str "FAILED: " cmd) out)))
    out))


(defn which [cmd]
  (let [{:keys [exit out]} (sh "which" cmd)]
    (when (= exit 0)
      (str/trim out))))


(defn which-tool
  [cmd url]
  (or (which cmd)
     (throw (ex-info (str "Missing required tool '" cmd "'. "
                          "please install it and add it to the $PATH. "
                          "see: " url) {}))))

(defn now []
  (.getTime (js/Date.)))


(defn wait-for
  "It waits for a given endpoint to come alive and reply HTTP 200"
  [name endpoint
   & {:keys [max-seconds interval] :as opts
      :or {max-seconds 60 interval 1}}]
  (msg "Waiting for" name "at" endpoint)
  (let [start (now)]
    (loop []
      (when
          (-> (try (http/get endpoint {:timeout 1})
                   (catch :default _ nil))
              :status
              (not= 200))
        (msg "   ... retrying in:" interval "second(s).")
        (sleep (* interval 1000))
        (if (< (- (now) start) (* 1000 max-seconds))
          (recur)
          (throw (ex-info (str "Timed out waiting for:" name)
                          {:name name :endpoint endpoint} )))))
    (msg "... ready!")))

;; (wait-for "foo" "http://127.0.0.1:3000" :max-seconds 10)


(defn ps [& {:keys [pid]}]
  {:pre [pid]}
  (as-> (sh "ps" "-p" (str pid)) $
    (when (= 0 (:exit $))
      (str/trim (:out $)))))


(defn kill [pid]
  {:pre [pid]}
  (:exit (sh "kill" "-9" (str pid))))



(defn read-config-file [file]
  (msg "Reading config from:" file)
  (-> (slurp file)
      (read-string)))


(defn option-config [[opt file :as  args]]
  (when (and (= opt "-c") (not (nil? file)))
    (read-config-file file)))


(defn sh-background-pid
  [cmd]
  (let [script-cnt (gstring/format "#!/bin/bash\n\n%s &\necho $!\n" cmd)
        script (path "/tmp" (str "script-" (random-uuid) ".sh"))]
    (spit script script-cnt)
    (debug "script:" script)
    (try
      (->> (sh! (str "bash " script))
           :out
           (js/parseInt))
      (finally
        (try (io/delete-file script)
             (catch :default _))))))


(defn jstat [{:keys [jstat]} pid outfile]
  (sh-background-pid
   (gstring/format "%s -gccause %d 1000 &> %s" jstat pid outfile)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                                                            ;;
;;                    ----==| O P E R A T I O N S |==----                     ;;
;;                                                                            ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn path-exists?
  ([path]
   (path-exists? path :any))
  ([path path-type]
   (let [{:keys [type] :as f} (-> path io/file io/file-attributes)]
     (if (= path-type :any)
       (boolean f)
       (and f (= type path-type))))))


(defn build-project
  [{:keys [base] :as config} project]
  (msg "Building" project)
  (with-sh-dir (path base project)
    (sh! "lein do clean, uberjar"))
  (let [output (path base project "target" (str project ".jar"))]
    (when (path-exists? output)
      (msg "Building" project "OK!")
      output)))


(defn build-all-servers [{:keys [servers] :as config}]
  (doseq [server servers]
    (build-project config server)))

;; (build-all-servers CFG)

(defn wrk
  [{:keys [threads connections duration timeout rate script url wrk] :as cfg
    :or {threads 3 timeout "1s"}}]
  {:pre [threads connections duration timeout rate url wrk]}
  (let [script (if script (gstring/format "-s %s" script) "")
        cmd    (gstring/format "%s -t%d -c%d --timeout %s -d%s -R%d -L %s %s"
                               wrk threads connections timeout duration rate script url)]
    (sh! cmd)))


;;(prn (wrk {:wrk (which "wrk") :connections 500 :rate 500 :duration "10s" :url "http://127.0.0.1:3000/"}))


;; discard the output
(defn run-warmup [{:keys [req-x-sec connections warmup-time script base] :as config} endpoint]
  (let [script (if (and script (not (str/starts-with? script "/")) ) (path base script) script)]
    (wrk {:wrk (:wrk config) :connections connections :rate req-x-sec
          :duration warmup-time :script script :url endpoint})))


(defn run-load [{:keys [req-x-sec connections duration script base] :as config} endpoint result-file]

  (let [script (if (and script (not (str/starts-with? script "/")) ) (path base script) script)]
    (->>
     (wrk {:wrk (:wrk config) :connections connections :rate req-x-sec
           :duration duration  :script script :url endpoint})
     :out
     (spit (and (io/make-parents result-file) result-file)))))



(defn run-java-with-pid [{:keys [java jvm-opts jar-name gc-log-file] :as cfg
                          :or {gc-log-file "./gc-log-file.log"}}]
  {:pre [java jvm-opts jar-name]}
  (let [props (str/join " " jvm-opts)
        props (str/replace props #"\$\{GC_LOG_FILE\}" gc-log-file)
        javacmd  (gstring/format "%s %s -jar %s &> /dev/null" java props jar-name)]
    (sh-background-pid javacmd)))


(defn start-server
  [config server]

  (msg "Starting server:" server)

  (let [_ (io/make-parents (:gc-log-file config))
        pid
        (run-java-with-pid
         (-> config
             (select-keys [:java :jvm-opts :gc-log-file])
             (assoc :jar-name (path (:base config) server "target"  (str server ".jar")))))]

    (sleep 1000)
    (if-not (ps :pid pid)
      (throw (ex-info "server didn't start" {:server server}))
      (let [statfile (str/replace-first (:gc-log-file config) #"(\.gclog)?$" ".gcstat")
            _ (io/make-parents statfile)
            jst-pid (jstat config pid statfile)]
        (msg "started server with pid:" pid "and jstat with:" jst-pid)
        (wait-for server (:endpoint config))
        {:pid pid :jstat-pid jst-pid}))))


(defn run-test
  [{:keys [endpoint base ts] :as config} server test]
  (msg {:type :big} "Running test:" test "on" server ", please wait.")
  (let [test-conf (get-in config [:test-conf test])
        gc-log (path base "results" ts (name test) (str server ".gclog"))
        config (merge config test-conf {:gc-log-file gc-log})
        pid    (start-server config server)
        _      (msg "Warmup server:" server)
        _      (run-warmup config endpoint)
        _      (sleep 2000)
        _      (msg "Run test on server:" server)
        result (path base "results" ts (name test) (str server ".out"))
        _      (run-load config endpoint result)
        _      (msg "Shutting down server:" server)
        _      (kill (:pid pid))
        _      (kill (:jstat-pid pid))]
    result))


(defn write-hardware-info
  [{:keys [hw-info hw-info-opts base ts] :as config}]
  (msg "Writing hardware info")
  (let [info-file (path base "results" ts "hardware-info.txt")]
    (->> (sh! (str hw-info " " hw-info-opts))
         :out
         (spit (and (io/make-parents info-file) info-file)))))


(defn write-system-info
  [{:keys [java base ts] :as config}]
  (let [info-file (path base "results" ts "sysconfig.txt")
        jver    (:err (sh! (str java " -version")))
        ulimit  (:out (sh! "ulimit -a"))
        sysctl  (:out (sh! "sysctl -A"))
        sep     (apply str (repeat 20 "-"))
        sepline (fn [section] (str sep " " section " " sep))]
    (->> [(sepline "java")
          jver
          (sepline "ulimit")
          ulimit
          (sepline "sysctl")
          sysctl
          ""]
         (str/join "\n")
         (spit (and (io/make-parents info-file) info-file)))
    info-file))


(defn run-all-tests
  [{:keys [endpoint base ts] :as config} & {:keys [servers tests]}]
  (msg {:type :huge} "Starting benchmarks" ts)
  (write-hardware-info config)
  (write-system-info   config)
  (->> (for [test   (or tests   (:tests config))
           server (or servers (:servers config))]
         [config server test])
       (run! (partial apply run-test)))
  ;; completed, now advise how to create graphs
  (msg (str "run:\n\t ./bin/plot-all.sh ./results/" ts
            "\n\t To create the latency graphs.")))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                                                            ;;
;;                        ----==| C O N F I G |==----                         ;;
;;                                                                            ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn config [cfg]
  (-> {;;Project base-dir
       :base (->> (sh "pwd") :out str/trim (#(str/replace % #"/bin$" "")))

       ;; location of the java executable
       :java (which-tool "java" "http://www.oracle.com/technetwork/java/javase/downloads/index.html")
       :jstat (which-tool "jstat" "http://www.oracle.com/technetwork/java/javase/downloads/index.html")

       ;; location of the wrk2 executable
       :wrk (which-tool "wrk" "https://github.com/giltene/wrk2")

       ;; location of the leiningen executable
       :lein (which-tool "lein" "https://leiningen.org")

       ;; hardware-info tool loocation
       :hw-info (which-tool "neofetch" "https://github.com/dylanaraps/neofetch/wiki/Installation")
       ;; command line options for the hw-info tool
       :hw-info-opts "--stdout"

       ;; timestamp
       :ts (format-date-generic "yyyy-MM-dd_HH-mm-ss" (js/Date.))

       ;; list of servers to test
       :servers ["aleph-server" "httpkit-server" "jetty-server"]

       ;; Throughput to test req x sec
       :req-x-sec 500

       ;; number of connections
       :connections 500

       ;; time to warm up the service before the test
       :warmup-time "10s"

       ;; duration of the actual test
       :duration "300s"

       ;; java options for the server
       :jvm-opts ["-server" "-Xmx1G" "-Xms1G" "-XX:+UseG1GC"
                  ;;"-Xlog:gc:file=${GC_LOG_FILE}" ;; java10
                  "-verbose:gc" "-XX:+PrintHeapAtGC" "-XX:+PrintGCDetails"
                  "-XX:+PrintGCCause" "-XX:+PrintGCDateStamps"
                  "-XX:+PrintTenuringDistribution"
                  "-Xloggc:${GC_LOG_FILE}"
                  ]

       ;; server port
       :endpoint "http://127.0.0.1:3000/"

       ;; tests to run
       :tests [:simple-get :post-1k :post-4k]

       ;; test configuration
       :test-conf
       {:post-1k {:script "./scripts/post1k.lua"}
        :post-4k {:script "./scripts/post4k.lua"}}
       }
      (merge cfg)))


;;
;; define configuration
;;
(def CFG (config (option-config *command-line-args*)))

;;
;; build uberjars
;;
(build-all-servers CFG)

;;
;; run tests
;;
(run-all-tests CFG)


(comment

  (build-all-servers CFG)
  (run-all-tests CFG :servers ["jetty-server"] :tests [:post-4k])

  (run-test CFG "aleph-server" :simple-get)
  (run-test CFG "httpkit-server" :simple-get)
  (run-test CFG "jetty-server" :simple-get)

  )
